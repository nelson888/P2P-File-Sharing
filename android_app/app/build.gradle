apply plugin: 'com.android.application'

def admobProperties = new Properties()
admobProperties.load(new FileInputStream(rootProject.file("admob.properties")))

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.tambapps.p2p.peer_transfer.android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 14
        versionName "4.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [admobApplicationId: admobProperties['admob.application.id']]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ACTION_FINISHED_INTERSTITIEL_ID", '"' + admobProperties['admob.interstitiel.id'] + '"'
        }

        debug {
            // test interstitiel id
            buildConfigField "String", "ACTION_FINISHED_INTERSTITIEL_ID", '"ca-app-pub-3940256099942544/1033173712"'
        }
    }
    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }
    buildToolsVersion = '28.0.3'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    // for speer
    implementation group: 'org.slf4j', name: 'slf4j-android', version: '1.7.30'

    // Crashlytics Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.android.gms:play-services-ads:20.1.0'


    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'